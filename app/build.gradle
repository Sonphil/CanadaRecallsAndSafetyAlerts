apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply from: rootProject.file('android.gradle')

def keyPropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(keyPropertiesFile.newReader())

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = Integer.valueOf(System.getenv("GITHUB_RUN_NUMBER") ?: 0)
println("versionBuild: " + versionBuild)

android {
    defaultConfig {
        applicationId "com.sonphil.canadarecallsandsafetyalerts"
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
            storeFile rootProject.file("./" + keystoreProperties['releaseKeyStore'])
            storePassword keystoreProperties['releaseStorePassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [enableCrashReporting: "true"]
        }
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    // Support libraries
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "com.google.android.material:material:$versions.material_components"
    implementation "androidx.browser:browser:$versions.support"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraint_layout"
    implementation "androidx.core:core-ktx:$versions.android_ktx"
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation "androidx.preference:preference-ktx:1.1.1"

    // Architecture components
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle_viewmodel_ktx"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"

    // Test
    testImplementation "junit:junit:$versions.junit"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:$versions.work_manager"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:$versions.firebase")
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-crashlytics"

    // Glide
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"

    // ScrollGalleryView
    implementation ('com.alibaba.android:ultraviewpager:1.0.7.8@aar') {
        transitive = true
    }

    // Dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    implementation "com.google.dagger:dagger-android:$versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.dagger"

    // Android Debug Database
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'

    // Toasty
    implementation 'com.github.GrenderG:Toasty:1.4.2'

    // RecyclerView Animators
    implementation 'jp.wasabeef:recyclerview-animators:3.0.0'

    implementation project(":domain")
    implementation project(":data")
}
